apply plugin: "java-library"
apply plugin: "maven-publish"

ext.majorVersion = 7
ext.minorVersion = 0
ext.minecraftVersion = "1.15.2"
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Suppiled by Jenkins
ext.buildNumber = System.env.BUILD_NUMBER == null ? "x"                : "$System.env.BUILD_NUMBER"
ext.mavenDirectory = System.env.MAVEN_DIR == null ? "$projectDir/repo" : "$System.env.MAVEN_DIR"

// Version used for distribution. Different from maven repo
group = "cat.nyaa"
archivesBaseName = "Yasui-mc$minecraftVersion"
version = "$majorVersion.$minorVersion.$buildNumber"

repositories {
    jcenter()
    maven { name 'Spigot';      url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype';    url 'https://oss.sonatype.org/content/groups/public' }
    maven { name 'NyaaCat';     url 'https://ci.nyaacat.com/maven/' }
    maven { name 'sk89q';       url 'https://maven.sk89q.com/repo/' }
    maven { name 'papermc';       url 'https://papermc.io/repo/repository/maven-public/' }
}


dependencies {
    // spigot
    compileOnly "com.destroystokyo.paper:paper-api:$minecraftVersion-R0.1-SNAPSHOT"
    compileOnly ('com.sk89q.worldedit:worldedit-bukkit:7.0.1-SNAPSHOT') {
        exclude group: 'io.papermc', module: 'paperlib'
        exclude group: 'org.bstats.bStats-Metrics', module: 'bstats-bukkit'
        exclude group: 'org.bukkit', module: 'bukkit'
        exclude group: 'com.destroystokyo.paper', module: 'paper-api'
        exclude group: 'org.mozilla', module: 'rhino'
        exclude group: 'de.schlichtherle', module: 'truezip'
    }
    // other nyaa plugins
    if (gradle.hasProperty("useLocalDependencies") && gradle.useLocalDependencies) {
        compileOnly project(":NyaaCore")
        compileOnly project(":NyaaUtils")
    } else {
        compileOnly('cat.nyaa:nyaacore:7.0-SNAPSHOT')
        compileOnly('cat.nyaa:nyaautils:7.0-SNAPSHOT')
    }

    // 3rd party plugins
    //compileOnly 'com.udojava:EvalEx:2.1' // NyaaCore shadows this

    // annotation processor
}

// publications
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}
processResources { // modify version string
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            groupId "cat.nyaa"
            artifactId "yasui"
            version "$majorVersion.$minorVersion-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "${mavenDirectory}"
        }
    }
}

